let iconburstDomains = externaldata(domain: string) [@"https://raw.githubusercontent.com/reversinglabs/iconburst-iocs/master/domains.txt"] with(ignoreFirstRecord=false, format="txt");
let iconburstDomainsList = iconburstDomains
| summarize make_list(domain);
(union isfuzzy=true 
(CommonSecurityLog
| where (isnotempty(DestinationHostName) and DestinationHostName has_any (iconburstDomainsList)) 
  or (isnotempty(RequestURL) and (RequestURL has_any (iconburstDomainsList)))
| extend timestamp = TimeGenerated, HostCustomEntity = SourceHostName),
(DeviceNetworkEvents
| where isnotempty(RemoteUrl)
| where RemoteUrl has_any (iconburstDomainsList)
| extend timestamp = TimeGenerated, HostCustomEntity = DeviceName, IPCustomEntity = RemoteIP),
(DnsEvents 
| extend DestinationIPAddress = IPAddresses, DNSName = Name, Host = Computer
| where DNSName has_any (iconburstDomainsList) 
| extend timestamp = TimeGenerated, IPCustomEntity = DestinationIPAddress, HostCustomEntity = Host),
(VMConnection 
| parse RemoteDnsCanonicalNames with * '["' DNSName '"]' *
| where isnotempty(DNSName)
| where DNSName has_any (iconburstDomainsList)
| extend timestamp = TimeGenerated , HostCustomEntity = Computer)
)